plugins {

    id 'java'
    id 'com.gradle.plugin-publish' version '0.14.0'
    id 'java-gradle-plugin'
}

apply from: 'gradle/property_loader.gradle'
def buildNumber = System.getenv('BUILD_NUMBER') ? System.getenv('BUILD_NUMBER') : System.getenv('TRAVIS_BUILD_NUMBER') ? System.getenv('TRAVIS_BUILD_NUMBER') : '0'
project.version = "${project.version}.${buildNumber}".toString()

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {

    mavenCentral()
}

dependencies {

    implementation gradleApi()

    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.9'
    implementation group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.13'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
    implementation group: 'com.google.guava', name: 'guava', version: '31.0.1-jre'
}

gradlePlugin {

    plugins {
    
        curseforgegradle {
        
            id = project.group
            displayName = project.archiveBaseName
            description = project.description
            implementationClass = "${project.group}.${project.archiveBaseName}Plugin"
        }
    }
}

pluginBundle {

    website = project.website
    vcsUrl = project.source
    description = project.description
    tags = gradle_tags.split(', ')

    plugins {
    
        curseforgegradle {
        
            displayName = project.archiveBaseName
        }
    }
    
    mavenCoordinates {
    
        groupId = project.group.toString()
        artifactId = project.name
        version = project.version.toString()
    }
}