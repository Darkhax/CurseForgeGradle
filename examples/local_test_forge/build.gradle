buildscript {
    repositories {
        maven {
            url = 'https://maven.minecraftforge.net'
        }

        mavenCentral()
    }

    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true

        // When adding a plugin from a local file all deps must be added manually.
        classpath group: 'com.google.code.gson', name: 'gson', version: '2.8.9'
        classpath group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.13'
        classpath group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
        classpath group: 'com.google.guava', name: 'guava', version: '31.0.1-jre'
        classpath files { file('../../build/libs').listFiles() }
    }
}

apply plugin: 'net.minecraftforge.gradle'

version = "1.0.${Math.abs(new Random().nextInt())}"
project.base.archivesName = 'CurseForgeGradle-TEST'

java.toolchain.languageVersion = JavaLanguageVersion.of(16)
java.withSourcesJar()
java.withJavadocJar()

minecraft {
    mappings channel: 'official', version: '1.17.1'
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.17.1-37.0.108'
}

jar.finalizedBy('reobfJar')

// CurseForgeGradle
tasks.register('publishCurseForge', net.darkhax.curseforgegradle.TaskPublishCurseForge) {

    apiToken.set(curseforgegradle_testtoken)

    // The main file to upload
    def mainFile = upload(537663, jar)
    mainFile.releaseType.set('beta')
    mainFile.changelog.set('This is a test file. Please ignore it. The mod does nothing.')
    mainFile.changelogType.set('markdown')
    mainFile.addIncompatibility('bookshelf')

    // The sources JAR
    def sourcesFile = mainFile.withAdditionalFile(sourcesJar)
    sourcesFile.changelog.set(project.layout.buildDirectory.file('changelog.md').map(file -> file.asFile.text))

    // The JavaDoc JAR
    def javadocFile = mainFile.withAdditionalFile(javadocJar)
    javadocFile.changelog.set('This is a test JavaDoc file!')
    javadocFile.displayName.set("JavaDoc - ${version}")
}