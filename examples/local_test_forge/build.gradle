buildscript {
    repositories {    
        maven {             
            url = 'https://maven.minecraftforge.net'
        }
        
        mavenCentral()
    }
    
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        
        // When adding a plugin from a local file all deps must be added manually.
        classpath group: 'com.google.code.gson', name: 'gson', version: '2.8.9'
        classpath group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.13'
        classpath group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
        classpath group: 'com.google.guava', name: 'guava', version: '31.0.1-jre'
        classpath files { file('../../build/libs').listFiles()}
    }
}

apply plugin: 'net.minecraftforge.gradle'

version = "1.0.${Math.abs(new Random().nextInt())}"
archivesBaseName = 'CurseForgeGradle-TEST'

java.toolchain.languageVersion = JavaLanguageVersion.of(16)
java.withSourcesJar()
java.withJavadocJar()
    
minecraft {
    mappings channel: 'official', version: '1.17.1'
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.17.1-37.0.108'
}

jar.finalizedBy('reobfJar')

// CurseForgeGradle
apply plugin: 'net.darkhax.curseforgegradle'

import net.darkhax.curseforgegradle.TaskPublishCurseForge

task publishCurseForge(type: TaskPublishCurseForge) {

    apiToken = curseforgegradle_testtoken
    
    // The main file to upload
    def mainFile = upload(537663, jar)
    mainFile.setReleaseType('beta')
    mainFile.setChangelog('This is a test file. Please ignore it. The mod does nothing.')
    mainFile.setChangelogType('markdown')
    mainFile.addGameVersion('1.0.0')
    mainFile.addJavaVersion('Java 6')
    mainFile.addModLoader('Rift')
    mainFile.addIncompatibility('bookshelf')
    
    // The sources JAR
    def sourcesFile = mainFile.withAdditionalFile(sourcesJar)
    
    // The JavaDoc JAR
    def javadocFile = mainFile.withAdditionalFile(javadocJar)
    javadocFile.setChangelog('This is a test JavaDoc file!')
    javadocFile.setDisplayName("JavaDoc - ${version}")
}